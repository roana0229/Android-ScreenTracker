// see https://github.com/bintray/bintray-examples/blob/master/gradle-bintray-plugin-examples/android-gradle-3.0.0-maven-example/app/publish.gradle

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version VERSION_NAME

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/screen-tracker-1.0.0.aar")
            groupId 'org.roana0229.app'
            artifactId 'screentracker'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    def properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty("bintray_user")
    key = properties.getProperty("bintray_apikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        userOrg = 'roana0229'
        repo = 'Android-Library'
        name = 'Android-ScreenTracker'
        description = "Android screen tracking library"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/roana0229/Android-ScreenTracker.git'
        dryRun = false
        version {
            name = this.version
            desc = "stable release ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}